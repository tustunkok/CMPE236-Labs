RW		EQU	P2.1
E		EQU	P2.2
RS		EQU	P2.0

		ORG	00H
		SJMP	MAIN

		ORG	30H
MAIN:		MOV	R2, #38H ; 00111000
		ACALL	CMDWRT
		ACALL	DELAY

		MOV	R2, #0EH ; 00001110
		ACALL	CMDWRT
		ACALL	DELAY

		MOV	R2, #06H ; 00000110
		ACALL	CMDWRT
		ACALL	DELAY

		MOV	R2, #84H ; 10000100
		ACALL	CMDWRT
		ACALL	DELAY

		MOV	DPTR, #HELLO
		ACALL	WRT2SCRN

		SJMP	$

WRT2SCRN:	CLR	A
		MOVC	A, @A+DPTR
REPEAT:		MOV	R2, A
		ACALL	DATAWRT
		ACALL	DELAY
		INC	DPTR
		CLR	A
		MOVC	A, @A+DPTR
		JNZ	REPEAT
		RET

; SUBROUTINE COMMAND WRITE
; PARAMS: COMMAND (1-BYTE)
CMDWRT:		MOV	P1, R2		; SEND DATA
		CLR	RS		; SET COMMAND REG
		CLR	RW		; SET WRITE MODE
		SETB	E		; SEND LATCH PULSE
		ACALL	DELAY		; SEND LATCH PULSE
		CLR	E		; SEND LATCH PULSE
		RET

DATAWRT:	MOV	P1, R2		; SEND ASCII CHAR
		SETB	RS		; SET DATA REG
		CLR	RW		; SET WRITE MODE
		SETB	E		; SEND LATCH PULSE
		ACALL	DELAY		; SEND LATCH PULSE
		CLR	E		; SEND LATCH PULSE
		RET

DELAY:		PUSH	02
		MOV	R3, #10
HERE1:		MOV	R2, #255
HERE:		DJNZ	R2, HERE
		DJNZ	R3, HERE1
		POP	02
		RET

		ORG	0200H
HELLO:		DB	'HELLO, WORLD!', 0
		END