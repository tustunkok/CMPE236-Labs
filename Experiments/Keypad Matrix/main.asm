; GLOBAL 7SEG CONSTANTS
N0		EQU	11000000B		; #DEFINE N0 11000000B
N1		EQU	11111001B
N2		EQU	10100100B
N3		EQU	10110000B
N4		EQU	10011001B
N5		EQU	10010010B
N6		EQU	10000010B
N7		EQU	11111000B
N8		EQU	10000000B
N9		EQU	10010000B
NA		EQU	10001000B
NB		EQU	10000011B
NC		EQU	11000110B
ND		EQU	10100001B
NE_		EQU	10000110B
NF		EQU	10001110B

		ORG	00H
		SJMP	MAIN

		ORG	30H
MAIN:		MOV	P1, #0F0H

LP1:		MOV	A, P1
		ORL	A, #0FH
		CPL	A
		JZ	LP1

		MOV	R2, #00D			; FIND COLUMN
		ACALL	FIND_ROW_COL

		MOV	R2, A				; PASS COLUMN DATA TO DECODE_KEY
		ACALL	DECODE_KEY

		MOV	R2, A
		ACALL	WRT2LED

		SJMP	MAIN

; ==================================================
DECODE_KEY:	MOV	P1, #0FH
		PUSH	02H				; R2 HOLDS THE COLUMN NUMBER
		MOV	R2, #01D
		ACALL	FIND_ROW_COL
		POP	02H
		MOV	R3, A				; R3 HOLDS THE ROW NUMBER

		MOV	A, R3
		MOV	B, #04D

		MUL	AB
		ADD	A, R2

		MOV	DPTR, #MATRIX_LOOKUP
		MOVC	A, @A+DPTR
		RET

; ==================================================
FIND_ROW_COL:	CJNE	R2, #00H, PASS_ROW_DATA
		MOV	DPTR, #COL_DATA
		SJMP	CNT1
PASS_ROW_DATA:	MOV	DPTR, #ROW_DATA
CNT1:		MOV	R2, P1
		MOV	R3, #04D

LP2:		MOV	A, R3
		DEC	A
		MOVC	A, @A+DPTR
		CLR	C
		SUBB	A, R2
		JZ	FOUND
		DJNZ	R3, LP2
FOUND:		MOV	A, #04D
		CLR	C
		SUBB	A, R3
		RET

; ==================================================
WRT2LED:	MOV	DPTR, #LED_DATA
		MOV	A, #0FH
		CLR	C
		SUBB	A, R2
		JC	EXT1
		MOV	A, R2
		MOVC	A, @A+DPTR
		MOV	P2, A
EXT1:		RET

; ==================================================
		ORG	0200H
COL_DATA:	DB	70H, 0B0H, 0D0H, 0E0H
ROW_DATA:	DB	07H, 0BH, 0DH, 0EH
;MATRIX_LOOKUP:	DB	'1', '2', '3', 'A', '4', '5', '6', 'B', '7', '8', '9', 'C', '*', '0', '#', 'D'
MATRIX_LOOKUP:	DB	01, 02, 03, 0AH, 04, 05, 06, 0BH, 07, 08, 09, 0CH, 0EH, 00, 0EH, 0DH
LED_DATA:	DB	N0, N1, N2, N3, N4, N5, N6, N7, N8, N9, NA, NB, NC, ND, NE_, NF
		END